#!/bin/csh

set IOAPI_exist = `env | grep IOAPI | wc -l`
if (! $IOAPI_exist) then
   echo ' '
   echo ' Please setup the path for IOAPI library first and execute this script again'
   echo ' '
   echo '    e.g. setenv IOAPI /home/wdx/lib/x86_64/ifc-17.0.3/ioapi_3.1'
   echo ' '
   exit
endif

# type fmm -h for help
set exist_h = ` echo $argv | grep -e "-h" | wc -w `

if ($exist_h != 0) then
   goto usage
else
   goto checkarg
endif

cont:

if (! $create_makefile_only) then
# modify code in wrf
  cd wrfcmaq_twoway_coupler

  if (-d misc/orig) then
     rm -f -r misc/orig
  endif

  ls -1 */* > __tfile

  set count = `wc -l __tfile`

  @ n = 0

# copy twoway related files to the appropriate directory and keep an old copy
  while ($n < $count[1])
    @ n++
    set temp = `head -n $n __tfile | tail -n 1 | sed 's/\// /' `
    set directory = $temp[1]
    set file      = $temp[2]
    if (($directory != misc) && ($directory != script)) then
       if ($directory == external) then
          cp -p $directory/$file ../$directory/io_netcdf
       else
          if (-f ../$directory/$file) then
             if (! -d misc/orig) then
                mkdir misc/orig
             endif
             if (! -d misc/orig/$directory) then
              mkdir misc/orig/$directory
             endif
           mv ../$directory/$file misc/orig/$directory/$file
          endif
          cp -p $directory/$file ../$directory
       endif
    endif
  end

  rm -f __tfile

  mv ../clean misc/orig/clean
  cp -r clean ..

  mv ../Makefile misc/orig/Makefile
  cp -r Makefile ..

  cd ..

  cp cmaq/complex_number_module.F90 phys/complex_number_module.F
endif

# create Makefile.twoway

cd cmaq

if (-f mpif.h) then
   rm -f mpif.h
endif

cp ../wrfcmaq_twoway_coupler/misc/Makefile.twoway.part1 Makefile.twoway

set cpp_begin_line  = `grep -n cpp_flags Makefile | sed 's/:/ /' `
set cpp_end_line    = `grep -n lioapi Makefile | sed 's/:/ /' `
set first_line      = `grep -n INCLUDES Makefile | sed 's/:/ /' `
set second_line     = `grep -n SUBST_MPI Makefile | sed 's/:/ /' `
set third_line      = `grep -n SUFFIXES Makefile | sed 's/:/ /' `
set fourth_line     = `grep -n dependencies Makefile | sed 's/:/ /' `
set total_line      = `wc -l Makefile`

@ cpp_begin = $cpp_begin_line[1]
@ cpp_end   = $cpp_end_line[1] - 1

@ first     = $first_line[1]
@ second    = $second_line[1] - 1
@ second_p1 = $second_line[1] + 1
@ third     = $third_line[1] - 1
@ third_p4  = $third_line[1] + 4
@ fourth    = $fourth_line[1]
@ fifth     = $fourth + 2
@ all_line  = $total_line[1]

# this section deal with CPP flags
@ diff = 1 + $cpp_end - $cpp_begin

head -n $cpp_end Makefile | tail -n $diff | sed 's/-Dparallel/-Dparallel -Dtwoway/'  >> Makefile.twoway

echo "BASE_INC = ." >> Makefile.twoway
echo " "            >> Makefile.twoway

# this section deal with INC path and INCLUDE lines
@ diff = 1 + $second - $first

head -n $second Makefile | tail -n $diff  >> Makefile.twoway

echo '  -DSUBST_MPI=\"mpif.h\"' >> Makefile.twoway

# this section deal with object file declaration
@ diff = 1 + $third - $second_p1

head -n $third Makefile    | \
  tail -n $diff            | \
  sed 's/  complex_number_module.o// ' >> Makefile.twoway

cat ../wrfcmaq_twoway_coupler/misc/Makefile.twoway.part2 >> Makefile.twoway

# this section deal with rules
@ diff = 1 + $fourth - $third_p4

head -n $fourth Makefile | tail -n $diff >> Makefile.twoway

# this section include twoway code dependence but skip it from the Makefile
cat ../wrfcmaq_twoway_coupler/misc/Makefile.twoway.part3 >> Makefile.twoway

# this section include the rest of the dependence
@ diff = 1 + $all_line - $fifth

head -n $all_line Makefile | tail -n $diff >> Makefile.twoway

cd ..

set found_openmpi = `echo $LD_LIBRARY_PATH | grep openmpi  | wc -l `

set tline = `grep "FORMAT_FIXED    =" configure.wrf`
if (`echo $tline | grep form | wc -l `) then
   set compiler = gcc
else if (`echo $tline | grep Mfixed | wc -l `) then
   set compiler = pgi
else if (`echo $tline | grep FI | wc -l `) then
   set compiler = ifort
   if ($found_openmpi) then
      cat configure.wrf | sed 's/=       mpif90 -f90=/=       mpifort -f90=/' > __new
   else
      cat configure.wrf | sed 's/=       mpif90 -f90=/=       mpiifort -f90=/' > __new
   endif
   mv __new configure.wrf
else
   echo 'ABORT: Unknow compiler '
   exit
endif

if (! $create_makefile_only) then
# modify configure.wrf
  wrfcmaq_twoway_coupler/reconfigure
endif

exit

#---------------------------------------------------------------------
checkarg:

set count = $#argv
if ($count == 1) then
   set create_makefile_only = 1
else
   set create_makefile_only = 0
endif

goto cont

# -------------------------------------------------------------------------
usage:
echo ' '
echo ' assemble [ m ] '
echo ' '
echo '    where option m to instruct the process to produce Makefile.twoway'
echo '      in cmaq only'
echo ' '
echo ' If you have any comment/question/problem using this script/program, '
echo ' please contact David Wong at wong.david-c@epa.gov'
echo ' '

exit

